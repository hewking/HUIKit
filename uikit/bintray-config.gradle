ext {
    //bintray 网站上你创建的仓库的名字（必配项）
    bintrayRepo = 'uikit'
    //在这个仓库下的 package name（必配项）
    bintrayName = 'uikit'
    //以上两项均只是指向 bintray 网站上你的仓库和仓库下的package

    //publishedGroupId:artifact:libraryVersion 构成你开源库的唯一路径
    //例如：com.dasu.tv:tv:0.0.1，在build.gradle里就可以根据这个路径来compile依赖库了
    //以下三项均是必配项
    publishedGroupId = 'com.hewking.github'
    artifact = 'uikit'
    libraryVersion = '0.0.2'

    //以下三项只是对开源库的描述（应该不是必配项吧，没尝试过）
    libraryName = 'uikit'
    libraryDescription = '一些常用的UI 组件'
    siteUrl = 'https://github.com/hewking/HUIKit'

    //开源库对应的 github 地址，不知道可不可以不配，应该也是必配
    gitUrl = 'https://github.com/hewking/HUIKit.git'

    //开发者信息，也是必配的吧
    developerId = 'hewking'
    developerName = 'hewking'
    developerEmail = 'cjhcoder@qq.com'

    //这部分可以不用改，我也不大懂这些开源协议，但应该都一样
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}


/** 以下开始是将Android Library上传到JCenter的相关配置**/
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目主页
def siteUrl = ext.siteUrl
//项目的版本控制地址
def gitUrl = ext.gitUrl

//发布到组织名称名字，必须填写
group = ext.publishedGroupId
//发布到JCenter上的项目名字，必须填写
def libName = ext.artifact
// 版本号，下次更新是只需要更改版本号即可
version = ext.libraryVersion
/**  上面配置后上传至JCenter后的编译路径是这样的： compile 'group:libName:version'  **/

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links siteUrl
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jCenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name libName
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'hewking'
                        name 'hewking'
                        email 'cjhcoder@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = libraryDescription    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}